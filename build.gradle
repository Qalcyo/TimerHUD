buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        flatDir dirs: 'libs'
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = modVersion
group = modGroup
archivesBaseName = modBaseName

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = project.forgeVersion
    runDir = "run"
	replace("@VER@", project.version)
    mappings = project.mcpVersion
    makeObfSourceJar = false
}

// Required because Srg2Source depends on a snapshot of JDT that is no longer published on forge maven
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.eclipse.jdt' && details.requested.name == 'org.eclipse.jdt.core') {
            details.useVersion '3.10.0'
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    implementation 'org.projectlombok:lombok:1.18.18'
    provided files("libs/modcore.jar")
}

jar {
    archiveName = "$baseName.$extension"
    manifest {
        attributes "FMLCorePlugin": "net.wyvest.timer.forge.FMLLoadingPlugin",
                "ModSide": "CLIENT",
                "FMLCorePluginContainsFMLMod": "Yes, yes it does"
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info", "LICENSE.txt"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}